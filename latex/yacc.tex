\begin{verbatim}
//_______________________________________________
// relaxed simple ITL format specification
//_______________________________________________
start        : expr
            | start expr
            ;
//_______________________________________________
// expression of the script language
//_______________________________________________
expr        : message  ENDEXPR        
            | variabledecl ENDEXPR    
            | script            
            | ENDSCRIPT            
            ;
//_______________________________________________
// javascript and support
//_______________________________________________
script        : JSCRIPT            
            ;
//_______________________________________________
// messages specification (extends osc spec.)
//_______________________________________________
message        : address                    
            | address params            
            | address eval LEFTPAR messagelist RIGHTPAR
            | address eval variable        
            ;
messagelist : message                    
            | messagelist messagelistseparator message 
            ;
messagelistseparator    : COMMA
                        | COLON
                        ;
//_______________________________________________
// address specification (extends osc spec.)
address        : oscaddress                
            | relativeaddress            
            | urlprefix oscaddress        
            ;
oscaddress  : OSCADDRESS                
            ;
relativeaddress    : POINT oscaddress        
            ;
urlprefix    : hostname  UINT            
            | STRING COLON UINT            
            | IPNUM COLON UINT            
            ;
hostname    : HOSTNAME                    
            ;
identifier    : IDENTIFIER                
            | HOSTNAME                    
            | REGEXP                    
            ;
//_______________________________________________
// parameters definitions
// eval need a special case since messages are expected as argument
eval        : EVAL                
            | variable                
            | params variable        
            | params param            
            ;
params        : sparam                
            | params sparam            
            | mathexpr                
            | params mathexpr        
            ;
variable    : VARIABLE                
            | VARIABLEPOSTINC        
            | VARIABLEPOSTDEC        
            | VARIABLEPREINC        
            | VARIABLEPREDEC        
            ;
msgvariable    : VARSTART LEFTPAR message RIGHTPAR 
            ;
param    : number                
        | FLOAT                    
        | identifier            
        | STRING                
        ;
sparam    : expression            
        | LEFTPAR messagelist RIGHTPAR    
        | script            
        ;
//_______________________________________________
// math expressions
mathexpr    : param                            
            | variable                        
            | msgvariable                    
            | mathexpr ADD mathexpr            
            | mathexpr SUB mathexpr            
            | MINUS mathexpr                
            | mathexpr MULT mathexpr        
            | mathexpr DIV mathexpr            
            | mathexpr MODULO mathexpr        
            | LEFTPAR mathexpr RIGHTPAR     
            | MIN LEFTPAR mathmin RIGHTPAR    
            | MAX LEFTPAR mathmax RIGHTPAR    
            | LEFTPAR mathbool QUEST mathexpr COLON mathexpr RIGHTPAR 
            ;
mathmin     : mathexpr                        
            | mathmin mathexpr                
            ;
mathmax     : mathexpr                        
            | mathmax mathexpr                
            ;
mathbool     : mathexpr                        
            | NEG mathexpr                    
            | mathexpr EQ mathexpr             
            | mathexpr NEQ mathexpr         
            | mathexpr GREATER mathexpr     
            | mathexpr GREATEREQ mathexpr     
            | mathexpr LESS mathexpr         
            | mathexpr LESSEQ mathexpr         
            ;
//_______________________________________________
// variable declaration
variabledecl : varname EQUAL params    
            ;
varname        : IDENTIFIER            
            ;
//_______________________________________________
// misc
number        : UINT                    
            | INT                    
            ;
//_______________________________________________
// expression declaration
expression    : EXPRESSION    
            ;
\end{verbatim}
